# üõ† Aula 12 - Procedimentos

> Lembrando: Durante todo este curso, ser√° utilizado o **visualg** (visualizador de algoritmos) para poder desenvolver e executar os algoritmos em **Portugol**.

<br>

## **Rotina - parte 1**

- rotina √© um procedimento repetitivo
- s√£o ut√©is para evitarmos repeti√ß√£o de c√≥digo

**Sintaxe da Estrutura**

````
Procedimento nomeDoProcedimento()
Inicio
  Bloco
FimProcedimento
````

**Exemplo - Fazer Vitamina de Banana :**

- Se quiser fazer uma vitamina de banana, siga o procedimento abaixo:
- Procedimento para fazer uma vitamina de banana:
  - Pegar  2 bananas
  - Pegar 500ml de √°gua
  - Pegar 50g de leite
  - Coloca tudo no Liquidificador
  - Bate tudo com o Liquidificador por 05min

````
algoritmo "Fazer Vitamina de Banana"

var
  resposta: Caractere

  Procedimento fazerVitaminaDeBanana()
  Inicio
    EscrevaL("- Pegar 2 bananas")
    EscrevaL("- Pegar 500ml de √°gua")
    EscrevaL("- Pegar 50g de leite")
    EscrevaL("- Colocar tudo no Liquidificador")
    EscrevaL("- Bater tudo com o Liquidificador por 05min")
  FimProcedimento

inicio
  Escreva("Quer fazer uma vitamina de banana [S/N]? ")
  Leia(resposta)
  Se (resposta = "s") entao
    fazerVitaminaDeBanana()
  FimSe

fimalgoritmo
````

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Detector de Pesado :**

- Crie um programa que leia o nome e o peso de cinco pessoas, compare e detecte qual foi o maior
peso at√© o momento informe-o durante toda execu√ß√£o, ao final, informe o nome e o peso da pessoa mais
pesada cadastrada.

````
// SEM UTILIZAR PROCEDIMENTOS
// MUITA REPETI√á√ÉO DE C√ìDIGO (C√ìDIGO SUJO)

algoritmo "DETECTOR DE PESADO (SEM UTILIZAR PROCEDIMENTOS)"

var
  contador: Inteiro
  nome, pesado: Caractere
  peso, maiorPeso: Real

inicio
  LimpaTela
  Escreval("----------------------------------")
  Escreval(" D E T E C T O R  DE  P E S A D O ")
  Escreval(" Maior Peso at√© agora: ", maiorPeso, "kg")
  Escreval("----------------------------------")
  Para contador <- 1 ate 5 faca
    Escreva("Digite o nome: ")
    Leia(nome)
    Escreva("Digite o peso de ", nome, ": ")
    Leia(peso)
    Se (peso > maiorPeso) entao
      maiorPeso <- peso
      pesado <- nome
    FimSe
    LimpaTela
    Escreval("_---------------------------------")
    Escreval(" D E T E C T O R  DE  P E S A D O ")
    Escreval(" Maior Peso at√© agora: ", maiorPeso, "kg")
    Escreval("----------------------------------")
  FimPara
  LimpaTela
  Escreval("_---------------------------------")
  Escreval(" D E T E C T O R  DE  P E S A D O ")
  Escreval(" Maior Peso at√© agora: ", maiorPeso, "kg")
  Escreval("----------------------------------")
  Escreval("A pessoa mais pesado foi ", pesado, ", com ", maiorPeso, " quilos. ")

fimalgoritmo
````

````
// UTILIZANDO PROCEDIMENTOS
// MENOS REPETI√á√ÉO DE C√ìDIGO (C√ìDIGO LIMPO)

algoritmo "PR√ÅTICA 01 - DETECTOR DE PESADO (com Procedimento)"

var
  contador: Inteiro
  nome, pesado: Caractere
  peso, maiorPeso: Real

  Procedimento Topo()
  Inicio
    LimpaTela
    Escreval("----------------------------------")
    Escreval(" D E T E C T O R  DE  P E S A D O ")
    Escreval(" Maior Peso at√© agora: ", maiorPeso, "kg")
    Escreval("----------------------------------")
  FimProcedimento

inicio
  Topo()
  Para contador <- 1 ate 5 faca
    Escreva("Digite o nome: ")
    Leia(nome)
    Escreva("Digite o peso de ", nome, ": ")
    Leia(peso)
    Se (peso > maiorPeso) entao
      maiorPeso <- peso
      pesado <- nome
    FimSe
    Topo()
  FimPara
  Topo()
  Escreval("A pessoa mais pesado foi ", pesado, ", com ", maiorPeso, " quilos. ")

fimalgoritmo
````

<br>

## **Passagem de Par√¢metro**

- Em um procedimento voc√™ pode utilizar par√¢metros

- Par√¢metros s√£o vari√°veis que est√£o recebem alguma coisa como refer√™ncia

**Exemplo:**

- Fa√ßa um procedimento passando dois n√∫meros como par√¢metros para serem somados.

````
algoritmo "PASSAGEM DE PAR√ÇMETRO POR VALOR"

var
  X, Y: Inteiro

  Procedimento Soma(A, B: Inteiro)
  Inicio
    Escreval("Recebi o valor ", A)   // A recebeu o valor de X como refer√™ncia
    Escreval("Recebi o valor ", B)   // B recebeu o valor de Y como refer√™ncia
    Escreval("A soma entre os dois √© ", A + B)
  FimProcedimento

inicio
  X <- 5
  Y <- 3
  Soma(X, Y)

fimalgoritmo
````

<br>

## **Tipos de Passagem de Par√¢metros: Passagem de Par√¢metro Por Valor**

- √â um tipo de passagem na qual os par√¢metros do procedimento recebem valores como refer√™ncia

- No exemplo acima ocorre uma passagem de par√¢metro por valor 
(A e B s√£o par√¢metros que recebem o valor de X e Y como refer√™ncia)

- OBS‚ùï N√£o use o mesmo nome da vari√°vel refer√™ncia no par√¢metro.

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Verificador Par/√çmpar :**

- Desenvolva um algoritmo que leia um n√∫mero, verifique e informe se ele √© par ou √≠mpar.

- Utilize procedimentos com passagem de par√¢metro por valor.

````
algoritmo "VERIFICADOR PAR/√çMPAR"

var
  numero: Inteiro

  Procedimento ParOuImpar(valor: Inteiro)
  Inicio
    Se (valor%2 = 0) entao
      Escreval("O n√∫mero ", valor, " √© PAR")
    senao
      Escreval("O n√πmero ", valor, " √© √çMPAR")
    FimSe
  FimProcedimento

inicio
  Escreva("Digite um n√∫mero: ")
  Leia(numero)
  ParOuImpar(numero)
fimalgoritmo
````

<br>

## **Escopo**

- escopo √© o local onde uma determinada vari√°vel vai funcionar.

- existe o escopo global, quando uma vari√°vel funciona em todo o lugar do programa.

- exsite o escopo local, quando uma vari√°vel s√≥ funciona em um lugar espec√≠fico.

**Exemplo:**

- nesse exemplo, somente as vari√°veis N1 e N2 tem o escopo global, logo, funcionam em qualquer
parte do programa.

- j√° as vari√°veis A e B, s√£o par√¢metros do procedimento 'Rotina', portanto, s√£o de escopo local
e funcionam somente dentro do procedimento 'Rotina'.

- e as vari√°veis X e Y, s√£o declaradas dentro do procedimento 'Rotina, logo, s√£o de escopo local
e funcionam somente dentro do procedimento 'Rotina'.

````
algoritmo "ESCOPO"

var
  N1, N2: Inteiro

  Procedimento Rotina(A, B: Inteiro)
  var
    X, Y: Inteiro
  Inicio
    X <- A
    Y <- B
    EscrevaL("DENTRO DO PROCEDIMENTO 'ROTINA'")
    EscrevaL("Valor de N1: ", N1)
    EscrevaL("Valor de N2: ", N2)
    EscrevaL("Valor de A: ", A)
    EscrevaL("Valor de B: ", B)
    EscrevaL("Valor de X: ", X)
    EscrevaL("Valor de Y: ", Y)
  FimProcedimento

inicio
  N1 <- 5
  N2 <- 3
  Rotina(N1, N2)
  EscrevaL()
  EscrevaL("FORA DO PROCEDIMENTO 'ROTINA'")
  EscrevaL("Valor de N1: ", N1)
  EscrevaL("Valor de N2: ", N2)
  EscrevaL("Valor de A: ", A)    // Vai dar ERRO: vari√°vel 'A' n√£o foi encontrada
  EscrevaL("Valor de B: ", B)    // Vai dar ERRO: vari√°vel 'B' n√£o foi encontrada
  EscrevaL("Valor de X: ", X)    // Vai dar ERRO: vari√°vel 'X' n√£o foi encontrada 
  EscrevaL("Valor de Y: ", Y)    // Vai dar ERRO: vari√°vel 'Y' n√£o foi encontrada 

fimalgoritmo
````

<br>

## **Tipos de Passagem de Par√¢metros: Passagem de Par√¢metro Por Refer√™ncia**

- Nesse tipo de passagem, os par√¢metros receber√£o a pr√≥pria vari√°vel como refer√™ncia

- Podemos fazer uma analogia e dizer que os par√¢metros receber√£o o 'endere√ßo' da vari√°vel 
como refer√™ncia

<BR>

### **Exemplo - Comparando Os Tipos de Passagem de Par√¢metro (Por Valor x Por Refer√™ncia) :**

````
algoritmo "PASSAGEM DE PAR√ÇMTRO POR VALOR"

var
  X, Y: Inteiro

  Procedimento Soma (A, B: Inteiro)
  Inicio
    A <- A + 1
    B <- B + 2
    Escreval("Valor de A = ", A)     // = 5  | A recebeu o valor da vari√°vel X como refer√™ncia 
    Escreval("Valor de B = ", B)     // = 10 | B recebeu o valor da vari√°vel Y como refer√™ncia
    Escreval("Soma A + B = ", A+B)   // = 15 
  FimProcedimento

inicio
  X <- 4
  Y <- 8
  Soma (X, Y)
  Escreval("Valor de X = ", X)    // = 4  | O valor da vari√°vel X n√£o muda
  Escreval("Valor de Y = ", Y)    // = 8  | O valor da vari√°vel Y n√£o muda
  Escreval("Soma X + Y = ", X+Y)  // = 12
    
fimalgoritmo
````

````
algoritmo "PASSAGEM DE PAR√ÇMTRO POR REFER√äNCIA"

var
  X, Y: Inteiro

  Procedimento Soma (var A, B: Inteiro)  // para passagem de par√¢metro por refer√™ncia, acrescente 'var' antes dos par√¢metros
  Inicio
    A <- A + 1
    B <- B + 2
    Escreval("Valor de A = ", A)     // = 5  | A recebeu a pr√≥pria vari√°vel X como refer√™ncia 
    Escreval("Valor de B = ", B)     // = 10 | B recebeu a pr√≥pria vari√°vel Y como refer√™ncia
    Escreval("Soma A + B = ", A+B)   // = 15 
  FimProcedimento

inicio
  X <- 4
  Y <- 8
  Soma (X, Y)
  Escreval("Valor de X = ", X)    // = 5  | O valor da vari√°vel X muda
  Escreval("Valor de Y = ", Y)    // = 10  | O valor da vari√°vel Y muda
  Escreval("Soma X + Y = ", X+Y)  // = 15
    
fimalgoritmo
````

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Sequ√™ncia de Fibonacci :**

- Utilizando procedimento e passagem de par√¢metro, desenvolva um algoritmo que calcule e exiba 
os 15 primeiros termos da sequ√™ncia de fibonacci.

- A sequ√™ncia de fibonacci consiste em uma sequ√™ncia de n√∫meros na qual o primeiro e segundo elemento
√© o 1 e o e os elementos seguintes s√£o originados pela soma de seus dois antecessores, observe:

  - 1, 1, 2, 3, 5, 8, 13, 21, 34, 55, 89, 144, 233, 377, 610, 987, 1597, 2584, 4181...

````
algoritmo "SEQU√äNCIA DE FIBONACCI"

var
  contador, valor1, valor2: Inteiro

  Procedimento ProximoFibonacci(var A, B: Inteiro)
  var
    valor3: Inteiro
  Inicio
    valor3 <- A + B
    Escreva(valor3)
    A <- B
    B <- valor3
  FimProcedimento

inicio
  valor1 <- 1
  Escreva(valor1)
  valor2 <- 1
  Escreva(valor2)
  Para contador <- 3 ate 15 faca
    ProximoFibonacci(valor1, valor2)
  FimPara
  
fimalgoritmo
````