# üõ† Aula 13 - Fun√ß√µes

<br>

> Lembrando: Durante todo este curso, ser√° utilizado o **visualg** (visualizador de algoritmos) para poder desenvolver algoritmos em **Portugol**.

<br>

> üññ Essas anota√ß√µes foram feitas a partir das aulas do professor **Gustavo Guanabara**. <br>
> üëâ Link para acessar as v√≠deo-aulas e os materiais do curso: https://www.cursoemvideo.com/curso/curso-de-algoritmo/

<br>

## **O que s√£o Fun√ß√µes?**

- De maneira pragm√°tica, fun√ß√µes s√£o procedimentos que, ao final da estrutura, **retornam um valor**.

- Portanto, os conceitos que valem para os procedimentos tamb√©m s√£o v√°lidos para as fun√ß√µes:

  - Ambos s√£o **blocos de c√≥digo** que se **repetem** com certa frequ√™ncia, por isso s√£o important√≠ssimos para a constru√ß√£o de um **c√≥digo limpo**.

  - Possuem sintaxes bem semelhantes:

  ````
  Funcao nomeDaFuncao(): tipoDaFuncao           Procedimento nomeDoProcedimento()
  inicio                                        Inicio
    Bloco                                         Bloco
    Retorne()                                   FimProcedimento
  FimFuncao
  ````

  - Ambos podem utilizar as passagens de par√¢metros (tanto por valor como por refer√™ncia). 

  - Por esses motivos giram em torno dos mesmo conceitos: Escopo, Argumentos, Par√¢metros, etc...

<br>

## **Procedimento x Fun√ß√£o - Quais s√£o as diferen√ßas?**

- Como foi dito no in√≠cio: "fun√ß√µes s√£o procedimentos que, ao final da estrutura, **retornam um valor**.".

- Nos ***Procedimentos*** n√£o retorna-se um valor, neles ocorre simplesmente a execu√ß√£o de um bloco de c√≥digo

- J√° nas ***Fun√ß√µes*** retorna-se um valor, nelas um bloco de c√≥digo √© executado mas o objetivo √© retornar um valor ao final da fun√ß√£o.

<br>

**Exemplo - Somar Dois N√∫meros (Procedimento x Fun√ß√£o) :**

````
algoritmo "Somar Dois N√∫meros (utilizando PROCEDIMENTO)"

var
  numero1, numero2: Inteiro
  
  Procedimento Soma(A, B: Inteiro)
  Inicio
     Escreval("A soma entre ", A, " e ", B, " √© igual a ", A+B)
  FimProcedimento

inicio
  Escreva("Digite o primeiro valor: ")
  Leia(numero1)
  Escreva("Digite o segundo valor: ")
  Leia(numero2)
  
  Soma(numero1, numero2)

fimalgoritmo
````

````
algoritmo "Somar Dois N√∫meros (utilizando FUN√á√ÉO)"

var
  numero1, numero2, soma: Inteiro
  
  // Ao declarar uma fun√ß√£o, √© necess√°rio definir o tipo de dado que ela ir√° retornar
  
  Funcao Soma(A, B: Inteiro): Inteiro 
  Inicio
    Retorne(A+B)
  FimFuncao

inicio
  Escreva("Digite o primeiro valor: ")
  Leia(numero1)
  Escreva("Digite o segundo valor: ")
  Leia(numero2)
  
  soma <- Soma(numero1, numero2)
  
  Escreval("A soma entre ", numero1, " e ", numero2, " √© igual a ", soma)

fimalgoritmo
````

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Verificador Par/√çmpar :**

<br>

- Desenvolva um algoritmo que leia um n√∫mero, verifique e informe se ele √© par ou √≠mpar.

- Utilize uma fun√ß√£o para realizar essa verifica√ß√£o.

````
algoritmo "Verificador Par/√çmpar"

var
  numero: Inteiro
  retorno: Caractere
   
Funcao ParOuImpar(N: Inteiro): Caractere
Inicio
  Se (N%2 = 0) entao
    Retorne("PAR")
  senao
    Retorne("√çMPAR")
  FimSe
FimFuncao

inicio
  Escreva("Digite um n√∫mero: ")
  Leia(numero)
  
  retorno <- ParOuImpar(numero)
  
  EscrevaL("O n√∫mero ", numero, " √© um valor ", retorno)
  
fimalgoritmo
````

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Fatorial de Um N√∫mero :**

<br>

- Crie um algoritmo que leia um n√∫mero, calcule e informe o fatorial desse n√∫mero.

- Utilize uma fun√ß√£o para realizar essa c√°lculo.

````
algoritmo "Fatorial de Um N√∫mero"

var
  numero, F: Inteiro

Funcao Fatorial(N:Inteiro): Inteiro
var
  contador, resultado: Inteiro
Inicio
  resultado <- 1
  
  Para contador <- 1 ate N faca
    resultado <- resultado * contador
  FimPara
  
  Retorne resultado
FimFuncao

inicio
  Escreva("Digite um n√∫mero: ")
  Leia(numero)
  
  F <- Fatorial(numero)
  
  EscrevaL()
  Escreval("O valor de ", numero, "! √® igual a ", F)
  
fimalgoritmo
````

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Sequ√™ncia de Fibonacci :**

<br>

- Desenvolva um algoritmo que calcule e exiba na tela os 15 primeiros n√∫meros da sequ√™ncia de fibonacci.

- Utilize uma fun√ß√£o para realizar esse c√°lculo.

````
algoritmo "Sequ√™ncia de Fibonacci"

var
  contador, primeiroNumero, segundoNumero, terceiroNumero: Inteiro

Funcao ProximoFibonacci(var N1, N2: Inteiro):Inteiro
var
  N3: Inteiro
Inicio
  N3 <- N1 + N2
  N1 <- N2
  N2 <- N3

  Retorne(N3)
FimFuncao

inicio
  Escreva(primeiroNumero)

  segundoNumero <- 1
  Escreva(segundoNumero)

  Para contador <- 3 ate 15 faca
    terceiroNumero <- ProximoFibonacci(primeiroNumero, segundoNumero)
    
    Escreva(terceiroNumero)
  FimPara

fimalgoritmo
````

<br>

## **Fun√ß√µes do Visualg - Manipula√ß√£o de Strings**

<br>

 **site <- "CursoEmVideo"**

<br>

Fun√ß√£o | Retorno | O que faz?
:--------- | :------: | -------:
Compr(site) | 12 | Retorna o comprimento de uma string
Copia(site, 6, 2) | Em | Retorna a copia de uma string contando a partir de um intervalo (string, inicioDoIntervalo, quantidadeDeLetras)
Maiusc(site) | CURSOEMVIDEO | Coloca todas as letras de uma string em mai√∫sculas
Minusc(site) | cursoemvideo | Coloca todas as letras de uma string em min√∫sculas
Pos("Video", site) | 8 | Retorna a posi√ß√£o de uma determinada string ou letra dentro de uma string maior
Asc("C") | 67 | Retorna o c√≥digo espec√≠fico de uma letra
Carac("67") | C | Retorna a letra espec√≠fica de um c√≥digo

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Analisador de Nomes :**

<br>

- Desenvolva um algoritmo, utilizando as fun√ß√µes do visualg, que leia o nome do usu√°rio e:
  
  - Informe o total de letras do nome;
  
  - Exiba o nome em letras mai√∫sculas;
  
  - Exiba o nome em letras min√∫sculas;
  
  - Informe a primeira letra do nome;
  
  - Informe a √∫ltima letra do nome;
  
  - Informe a posi√ß√£o da letra "A" no nome;
  
  - Informe a c√≥digo correspondente a letra "A";
  
  - Informe a a letra correspondnete ao c√≥digo 65;

  - Monte e exiba na tela o nome do usu√°rio ao contr√°rio.

````
algoritmo "Analisador de Nomes"

var
  nome: Caractere
  Contador: Inteiro

inicio
  EscrevaL(" ANLISADOR DE NOMES ")
  EscrevaL("--------------------")

  Escreva("Digite seu nome: ")
  Leia(nome)

  EscrevaL()
  Escreval("-> Total de letras do seu nome √© ", Compr(nome))

  EscrevaL()
  Escreval("-> Seu nome em mai√∫sculas ", Maiusc(nome))

  EscrevaL()
  Escreval("-> Seu nome em min√∫sculas √© ", Minusc(nome))

  EscrevaL()
  Escreval("-> A primeira letra do seu nome √© ", Copia(Maiusc(nome), 1, 1))

  EscrevaL()
  Escreval("-> A √∫ltima letra do seu nome √© ", Copia(Maiusc(nome), Compr(nome), 1))

  EscrevaL()
  Escreval("-> Seu nome tem a letra A na posi√ß√£o ", Pos("A", Maiusc(nome)))

  EscrevaL()
  Escreval("-> O c√≥digo da letra A √© ", Asc("A"))

  EscrevaL()
  Escreval("A letra de c√≥digo 65 √© ", Carac(65))

  EscrevaL()
  Escreva("-> Seu nome ao contr√°rio √© ")
  Para Contador <- Compr(nome) ate 1 passo -1 faca
    Escreva(Copia(Maiusc(nome), Contador, 1))
  FimPara

  EscrevaL()

fimalgoritmo
````

<br><br>

<p align="center"> Desenvolvido com üíô por Elias de Ara√∫jo Ferreira Neto üëã <p>

<br>

<a href="../README.md">Voltar</a>
