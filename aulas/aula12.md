# üõ† Aula 12 - Procedimentos

<br>

> Lembrando: Durante todo este curso, ser√° utilizado o **visualg** (visualizador de algoritmos) para poder desenvolver algoritmos em **Portugol**.

<br>

> üññ Essas anota√ß√µes foram feitas a partir das aulas do professor **Gustavo Guanabara**. <br>
> üëâ Link para acessar as v√≠deo-aulas e os materiais do curso: https://www.cursoemvideo.com/curso/curso-de-algoritmo/

<br>

## **O que s√£o Procedimentos?**

- Procedimentos s√£o **blocos de c√≥digo** que se **repetem** com certa frequ√™ncia. 

- Eles s√£o muito √∫teis para escrevermos um c√≥digo mais organizado (mais **limpo**), pois elas ***evitam a repeti√ß√£o desnecess√°ria de c√≥digo***.

<br>

### **Sintaxe da Estrutura**

````
Procedimento nomeDoProcedimento()
Inicio
  Bloco
FimProcedimento
````

<br>

**Exemplo - Fazer Vitamina de Banana :**

- Toda vez que eu for fazer uma vitamina de banana eu posso, simplesmente, seguir um procedimento padr√£o:

  [x] Pegar  2 bananas
  
  [x] Pegar 500ml de √°gua

  [x] Pegar 50g de leite

  [x] Coloca tudo no Liquidificador

  [x] Bate tudo no Liquidificador por 5min
  
- Logo, quando eu quiser fazer uma vitamina de banana eu s√≥ preciso repetir um procedimento j√° definido previamente.

````
algoritmo "Fazer Vitamina de Banana"

var
  resposta: Caractere

  Procedimento fazerVitaminaDeBanana()
  Inicio
    EscrevaL("[x] Pegar 2 bananas")
    EscrevaL("[x] Pegar 500ml de √°gua")
    EscrevaL("[x] Pegar 50g de leite")
    EscrevaL("[x] Colocar tudo no Liquidificador")
    EscrevaL("[x] Bater tudo no Liquidificador por 5min")
  FimProcedimento

inicio
  Escreva("Quer fazer uma vitamina de banana [S/N]? ")
  Leia(resposta)
  
  EscrevaL()
  Se (resposta = "s") entao
    fazerVitaminaDeBanana()
  FimSe

fimalgoritmo
````

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Detector de Pesado :**

<br>

- Crie um programa que leia o nome e o peso de cinco pessoas e informe durante toda execu√ß√£o qual foi o maior
peso at√© o momento. Ao final, exiba o nome e o peso da pessoa mais pesada que foi cadastrada.

````
// ‚ùå SEM UTILIZAR PROCEDIMENTOS                                                         
// üëé MUITA REPETI√á√ÉO DE C√ìDIGO (C√ìDIGO SUJO)                                            
  
algoritmo "Detector de Pesado (SEM UTILIZAR PROCEDIMENTOS)"                              

var                                                                                    
  contador: Inteiro                                                                              
  nome, pesado: Caractere
  peso, maiorPeso: Real

inicio
  Para contador <- 1 ate 5 faca
    LimpaTela
    Escreval("_---------------------------------")
    Escreval(" D E T E C T O R  DE  P E S A D O ")
    Escreval(" Maior Peso at√© agora: ", maiorPeso, "kg")
    Escreval("----------------------------------")
  
    Escreva("Digite o nome: ")
    Leia(nome)
    Escreva("Digite o peso de ", nome, ": ")
    Leia(peso)
    
    Se (peso > maiorPeso) entao
      maiorPeso <- peso
      pesado <- nome
    FimSe
    
  FimPara

  LimpaTela
  Escreval("_---------------------------------")
  Escreval(" D E T E C T O R  DE  P E S A D O ")
  Escreval(" Maior Peso at√© agora: ", maiorPeso, "kg")
  Escreval("----------------------------------")
  Escreval("A pessoa mais pesado foi ", pesado, ", com ", maiorPeso, " quilos. ")

fimalgoritmo
````

````
// ‚úÖ UTILIZANDO PROCEDIMENTOS
// üëç MENOS REPETI√á√ÉO DE C√ìDIGO (C√ìDIGO LIMPO)

algoritmo "DETECTOR DE PESADO (UTILIZANDO PROCEDIMENTOS)"

var
  contador: Inteiro
  nome, pesado: Caractere
  peso, maiorPeso: Real

  Procedimento Topo()
  Inicio
    LimpaTela
    Escreval("----------------------------------")
    Escreval(" D E T E C T O R  DE  P E S A D O ")
    Escreval(" Maior Peso at√© agora: ", maiorPeso, "kg")
    Escreval("----------------------------------")
  FimProcedimento

inicio
  Para contador <- 1 ate 5 faca
    Topo()
    
    Escreva("Digite o nome: ")
    Leia(nome)
    Escreva("Digite o peso de ", nome, ": ")
    Leia(peso)
    
    Se (peso > maiorPeso) entao
      maiorPeso <- peso
      pesado <- nome
    FimSe
  FimPara
  
  Topo()
  Escreval("A pessoa mais pesado foi ", pesado, ", com ", maiorPeso, " quilos. ")

fimalgoritmo
````

<br>

## **Procedimentos com Passagem de Par√¢metros**

- Par√¢metro √© uma palavra que utilizamos, normalmente, para estabelecer uma **rela√ß√£o de compara√ß√£o**.

  - Por exemplo: "Muitos deliverys utilizam o IFood como par√¢metro." 

  - Nessa frase, "utilizar o IFood como par√¢metro" significa que muitos deliverys utilizam o IFood como **REFER√äNCIA** de neg√≥cio.

  - Na programa√ß√£o, a ideia √© semelhante...

- Um procedimento pode utilizar o **valor** ou o **"endere√ßo"** de uma vari√°vel como par√¢metro (ou seja, como refer√™ncia).

<br>

**Exemplo:**

- Fa√ßa um procedimento que possua dois par√¢metros num√©ricos e realize a soma deles.

````
algoritmo "Passagem de Par√¢metros"

var
  X, Y: Inteiro

  Procedimento Soma(A, B: Inteiro)
  Inicio
    Escreval("Recebi o valor ", A)   // A recebeu o valor da vari√°vel X como refer√™ncia
    Escreval("Recebi o valor ", B)   // B recebeu o valor da vari√°vel Y como refer√™ncia
    Escreval("A soma entre os dois √© ", A + B)
  FimProcedimento

inicio
  X <- 5
  Y <- 3
  Soma(X, Y) 

fimalgoritmo
````

- Perceba que A e B funcionam normalmente como vari√°veis **dentro** da estrutura do procedimento.

  - √â por isso que precisamos delcarar os par√¢metros da mesma forma como declaramos vari√°veis ("nomeDoPar√¢metro" + tipo de dado que ser√° armazenado).

- Na programa√ß√£o, muitas vezes h√° uma confus√£o entre o conceito de **PAR√ÇMETROS** e de **ARGUMENTOS**:

  - PAR√ÇMETROS s√£o as vari√°veis que ir√£o **receber** algo como refer√™ncia dentro de um procedimento (no exmeplo acima, A e B).

  - ARGUMENTOS s√£o aquilo que voc√™ **passa** como refer√™ncia para os par√¢metros de um procedimento (no exemplo acima, X e Y).

  - Ou seja, no exemplo acima, os *par√¢metros* A e B do *procedimento* Soma() est√£o recebendo X e Y como *argumentos*.

<br>

## **Passagem de Par√¢metro Por Valor**

- Como j√° foi dito: "Um procedimento pode utilizar o **valor** ou o **"endere√ßo"** de uma vari√°vel como par√¢metro (ou seja, como refer√™ncia)". 

- No exemplo anterior, ocorre uma passagem de par√¢metro por valor. 

  - A e B s√£o par√¢metros que recebem o **valor** dos argumentos X e Y como refer√™ncia.

- ***OBS‚ùï Use sempre nomes diferentes para os argumentos e os par√¢metros.***

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Verificador Par/√çmpar :**

<br>

- Desenvolva um algoritmo que leia um n√∫mero, verifique e informe se ele √© par ou √≠mpar.

````
algoritmo "Verificador Par/√çmpar"

var
  numero: Inteiro

  Procedimento ParOuImpar(N: Inteiro)  // O par√¢metro N recebe o valor do seu argumento como refer√™ncia
  Inicio
    Se (N%2 = 0) entao
      Escreval("O n√∫mero ", N, " √© PAR")
    senao
      Escreval("O n√πmero ", N, " √© √çMPAR")
    FimSe
  FimProcedimento

inicio
  Escreva("Digite um n√∫mero: ")
  Leia(numero)
  ParOuImpar(numero)  // Chamando o procedimento ParOuImpar() e Passando a vari√°vel n√∫mero como Argumento
  
fimalgoritmo
````

<br>

## **Escopo**

- Escopo √© um conceito crucial na programa√ß√£o.

- Ele representa o **local** onde uma determinada vari√°vel vai **funcionar**.

- Existem dois **tipos** de escopos:

  - **Escopo Global**, quando uma vari√°vel funciona em qualquer lugar do programa.

  - **Escopo Local**, quando uma vari√°vel s√≥ funciona num lugar espec√≠fico do programa.

**Exemplo:**

````
algoritmo "ESCOPO"

var
  N1, N2: Inteiro

  Procedimento Rotina(A, B: Inteiro)
  var
    X, Y: Inteiro
  Inicio
    X <- A
    Y <- B
    
    EscrevaL("DENTRO DO PROCEDIMENTO 'ROTINA'")
    
    EscrevaL("Valor de N1: ", N1)     // N√ÉO vai dar ERRO
    EscrevaL("Valor de N2: ", N2)     // N√ÉO vai dar ERRO
    EscrevaL("Valor de A: ", A)       // N√ÉO vai dar ERRO
    EscrevaL("Valor de B: ", B)       // N√ÉO vai dar ERRO
    EscrevaL("Valor de X: ", X)       // N√ÉO vai dar ERRO
    EscrevaL("Valor de Y: ", Y)       // N√ÉO vai dar ERRO
  FimProcedimento

inicio
  N1 <- 5
  N2 <- 3
  
  Rotina(N1, N2)
  
  EscrevaL()
  EscrevaL("FORA DO PROCEDIMENTO 'ROTINA'")
  
  EscrevaL("Valor de N1: ", N1)
  EscrevaL("Valor de N2: ", N2)
  EscrevaL("Valor de A: ", A)    // Vai dar ERRO: vari√°vel 'A' n√£o foi encontrada
  EscrevaL("Valor de B: ", B)    // Vai dar ERRO: vari√°vel 'B' n√£o foi encontrada
  EscrevaL("Valor de X: ", X)    // Vai dar ERRO: vari√°vel 'X' n√£o foi encontrada 
  EscrevaL("Valor de Y: ", Y)    // Vai dar ERRO: vari√°vel 'Y' n√£o foi encontrada 

fimalgoritmo
````

- Nesse exemplo:
  
  -  Somente as vari√°veis N1 e N2 t√™m **escopo global**, logo, funcionam em qualquer parte do programa.

  -  J√° as vari√°veis A e B s√£o par√¢metros do procedimento Rotina(), portanto, possuem **escopo local** (ou seja, funcionam somente dentro do procedimento Rotina()).

  -  Por fim, as vari√°veis X e Y s√£o declaradas dentro do procedimento Rotina(), consequentemente, tamb√©m possuem **escopo local** (ou seja, funcionam somente dentro do procedimento).

<br>

## **Passagem de Par√¢metro Por Refer√™ncia**

- No in√≠cio eu ressaltei que "Um procedimento pode utilizar o **valor** ou o **"endere√ßo"** de uma vari√°vel como par√¢metro (ou seja, como refer√™ncia)."

- Diferentemente da passagem de par√¢metro por valor, na passagem por refer√™ncia, os par√¢metos do procedimento est√£o **diretamente ligados** √†s vari√°vel que foram passadas como refer√™ncias (ou seja, aos Argumentos).

- Isso quer dizer que: se eu passar um argumento X com valor 1 para um par√¢metro A de um procedimento qualquer, por exemplo, e, dentro do procedimento, A tem seu valor reatribuido para 2, quando o procedimento chegar ao fim e retornar ao fluxo normal da aplica√ß√£o, o valor de X tamb√©m ter√° sido reatribu√≠do para 2, pois na passagem de par√¢metro por refer√™ncia os Argumentos do procedimento est√£o diretamente ligados aos seus Par√¢metros.

- √â como se os par√¢metros recebecem seus pr√≥prios argumentos como refer√™ncia. Podemos fazer uma analogia e dizer que os par√¢metros receber√£o o **"endere√ßo"** de seus argumentos como refer√™ncia, logo, o que √© alterado naquele afeta diretamente esse.

<br>

### **Exemplo - Comparando Os Tipos de Passagem de Par√¢metro (Por Valor x Por Refer√™ncia) :**

````
algoritmo "Passagem de Par√¢metros por REFER√äNCIA"

var
  X, Y: Inteiro

  // para passagem de par√¢metro por refer√™ncia, acrescente 'var' antes dos par√¢metros
  
  Procedimento Soma (var A, B: Inteiro)  // A e B (Par√¢metros do procedimento Soma) recebem os "ENDERE√áOS" de seus argumentos (X e Y, respectivamente) como refer√™ncia
  Inicio
    A <- A + 1  // A tem seu valor reatribu√≠do
    B <- B + 2  // B tem seu valor reatribu√≠do
    
    Escreval("Valor de A = ", A)     // = 5
    Escreval("Valor de B = ", B)     // = 10
    Escreval("Soma A + B = ", A+B)   // = 15 
  FimProcedimento

inicio
  X <- 4
  Y <- 8
  
  Soma(X, Y)  // Chamando procedimento Soma() e Passando X e Y como argumentos
  
  Escreval("Valor de X = ", X)    // = 5   | O valor da vari√°vel X muda
  Escreval("Valor de Y = ", Y)    // = 10  | O valor da vari√°vel Y muda
  Escreval("Soma X + Y = ", X+Y)  // = 15
    
fimalgoritmo
````

````
algoritmo "Passagem de Par√¢metros por VALOR"

var
  X, Y: Inteiro

  Procedimento Soma (A, B: Inteiro) // A e B (Par√¢metros do procedimento Soma) recebem os VALORES de seus argumentos (X e Y, respectivamente) como refer√™ncia
  Inicio
    A <- A + 1  // A tem seu valor reatribu√≠do
    B <- B + 2  // B tem seu valor reatribu√≠do
    
    Escreval("Valor de A = ", A)     // = 5
    Escreval("Valor de B = ", B)     // = 10
    Escreval("Soma A + B = ", A+B)   // = 15 
  FimProcedimento

inicio
  X <- 4
  Y <- 8
  
  Soma (X, Y)  // Chamando procedimento Soma() e Passando X e Y como argumentos
  
  Escreval("Valor de X = ", X)    // = 4  | O valor da vari√°vel X n√£o muda
  Escreval("Valor de Y = ", Y)    // = 8  | O valor da vari√°vel Y n√£o muda
  Escreval("Soma X + Y = ", X+Y)  // = 12
    
fimalgoritmo
````

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Sequ√™ncia de Fibonacci :**

<br>

- Utilizando procedimento, desenvolva um algoritmo que calcule e exiba os 15 primeiros termos da sequ√™ncia de fibonacci.

- A sequ√™ncia de fibonacci consiste em uma sequ√™ncia de n√∫meros na qual o primeiro e segundo elemento √© o 1 e o e os elementos seguintes s√£o originados pela soma de seus dois antecessores, observe:

  - 1, 1, 2, 3, 5, 8, 13, 21, 34, 55, 89, 144, 233, 377, 610, 987, 1597, 2584, 4181...

````
algoritmo "Sequ√™ncia de Fibonacci"

var
  contador, primeiroValor, segundoValor: Inteiro

  Procedimento ProximoFibonacci(var A, B: Inteiro)
  var
    terceiroValor: Inteiro
  Inicio
    terceiroValor <- A + B
    
    Escreva(terceiroValor)
    
    A <- B
    B <- terceiroValor
  FimProcedimento

inicio
  primeiroValor <- 1
  Escreva(primeiroValor)
  
  segundoValor <- 1
  Escreva(segundoValor)
  
  Para contador <- 3 ate 15 faca
    ProximoFibonacci(primeiroValor, segundoValor)
  FimPara
  
fimalgoritmo
````

<br><br>

<p align="center"> Desenvolvido com üíô por Elias de Ara√∫jo Ferreira Neto üëã <p>
