# üóÑ Aula 14 - Vetores (Vari√°veis Compostas - Parte 1)

<br>

> Lembrando: Durante todo este curso, ser√° utilizado o **visualg** (visualizador de algoritmos) para poder desenvolver algoritmos em **Portugol**.

<br>

> üññ Essas anota√ß√µes foram feitas a partir das aulas do professor **Gustavo Guanabara**. <br>
> üëâ Link para acessar as v√≠deo-aulas e os materiais do curso: https://www.cursoemvideo.com/curso/curso-de-algoritmo/

<br>

> At√© o momento s√≥ utilizamos vari√°veis simples. A partir de agora, iremos trabalhar com **_vari√°veis compostas_**.

<br>

### Relembrando

<br>

- ***Vari√°veis*** s√£o **espa√ßos** na mem√≥ria do computador que servem para **armazenar dados**.

- **Vari√°veis Simples** s√£o vari√°veis que s√≥ podem armazenar um √∫nico dado.

- Vamos utilizar a analogia que utilizamos no in√≠cio do curso na qual consideramos a mem√≥ria do computador sendo um grande "arm√°rio" e os espa√ßos desse arm√°rio sendo as vari√°veis. 

- Olha s√≥ o que acontece quando declaramos e atribuimos valores em vari√°veis simples: 

<br>

<div align="center">
  <img align="center" width="500px" src="./images/vetores.gif">  
</div>

<br>

- Reparou que as vari√°veis foram **dispostas de forma BAGUN√áADA** na mem√≥ria do computador? √â justamente isso que acontece com as vari√°veis simples, elas s√£o alocadas conforme a necessidade do sistema operacional.

- Com **vetores** as coisas funcionam um pouco diferente...

<br>

## **O que s√£o Vetores?**

- Vetores, nada mais s√£o, que **vari√°veis** (ou seja, servem para armazenar dados na mem√≥ria do computador). Por√©m, n√£o s√£o vari√°veis simples mas sim **vari√°veis compostas**.

   - Isso quer dizer que um √∫nico vetor √© composto por **mais de um espa√ßo** na mem√≥ria do computador.

- Olha s√≥ como funciona a **declara√ß√£o** de um vetor, sua **aloca√ß√£o** na mem√≥ria do computador e sua **atribui√ß√£o** de valores:

<br>

<div align="center">
  <img align="center" width="500px" src="./images/vetores(1).gif">  
</div>

<br>

- O que podemos observar:

   - Um vetor j√° se diferencia de uma vari√°vel simples na **declara√ß√£o**.

      - A declara√ß√£o de um vetor √© composta por: {nomeDaVariavel + vetor[quantidadeDeEspa√ßosDispon√≠veis] + tipoDaVariavel}.

   - A **aloca√ß√£o** de um vetor √© feita de maneira **organizada** e **sequencial**, direntemente do que ocorre com as vari√°veis simples.

   - Para **atribuir valores** num vetor tamb√©m funciona de maneira um pouco diferente das vari√°veis simples.

      - A atribui√ß√£o de valor num vetor exige: {nomeDaVariavel[inidice]}

      - Como j√° sabemos, um vetor √© uma vari√°vel composta (portanto, √© formado por mais de um espa√ßo na mem√≥ria do computador),
      por esse motivo que, ao atribui valores num vetor, √© necess√°rio colocar o nome do vetor e (entre colchetes) o que chamamos de **√çNDICE**.

      - O √≠ndice serve para indicar uma **posi√ß√£o** espec√≠fica dentro de um vetor.
   
   > Por fim, √© not√≥rio o grande poder de um vetor: ao inv√©s de utilizarmos 4 vari√°veis para armazenar 4 valores, 
   > com os vetores, √© preciso somente 1 vari√°vel para armazenar 4 valores (ou mais).

<br>

## **Vari√°vel Simples X Vari√°vel Composta**

<br>

| Vari√°vel Simples | Vari√°vel Composta |
| ---------------------------------- | -------------------------------------- |
| Capacidade para armezanar um √∫nico valor | Capacidade para armazenar mais de um valor |
| Composta por um √∫nico espa√ßo na mem√≥ria | Composta por mais de um espa√ßo na mem√≥ria |
| S√£o alocadas de forma bagun√ßada | S√£o alocadas de forma ordenada e sequencial |

- Suas declara√ß√µes e atribui√ß√µes  de valores s√£o diferentes tamb√©m.

<br>

## **Vetores - Vari√°veis Compostas Homog√™neas Unidimensionais**

- Tecnicamente falando, os vetores s√£o chamados de um nome bem maior: Vari√°veis Compostas Homog√™neas Unidimensionais

- N√£o precisa se assustar, cada palavra desse nome tem um porqu√™:

  - **Vari√°veis**: Pois os vetores s√£o espa√ßos ("gavetas") na mem√≥ria do computador ("arm√°rio") que servem para armazenar dados.

  - **Compostas**: Pois os vetores s√£o vari√°veis compostas por mais de um espa√ßo na mem√≥ria do computador.

  - **Homeg√™neas**: Pois os espa√ßos que comp√µe um vetor armazenar√£o dados do mesmo tipo (lembra que vimos que, ao declarar um vetor, foi preciso informar o tipo dele e que ele foi composto, justamente, apenas com dados desse tipo?).

  - **Unidemensionais**: Pois para identificar cada um dos espa√ßos de um vetor √© necess√°rio somente um √≠ndice (somente um "endere√ßo" para identificar uma posi√ß√£o dentro do vetor).

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Construindo Um Vetor :**

<br>

- Desenvolva um algoritmo que leia 5 n√∫meros, armazene-os dentro de um vetor e exiba esse vetor na tela.

<br>

```
algoritmo "Construindo Um Vetor"

var
   v: vetor[1..5] de inteiro
   i: inteiro

inicio
   Para i <- 1 ate 5 faca
      Escreva("Digete o ", i, "¬∞ n√∫mero: ")
      Leia(v[i])
   FimPara

   EscrevaL()
   Escreva("Vetor Completo: ")
   Para i <- 1 ate 5 faca
      Escreva("{", v[i], "}")
   FimPara

fimalgoritmo
```

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Total de Valores Pares :**

<br>

- Crie um algoritmo que leia sete n√∫meros, armazene-os dentro de um vetor, analise quais desses n√∫meros s√£o pares e informe qual a posi√ß√£o desses n√∫meros e o total de n√∫meros pares encontrado.

<br>

```
algoritmo "Total de Valores Pares"

var
  n: vetor[1..7] de Inteiro
  i, totPar: Inteiro

inicio
   Para i <- 1 ate 7 faca
      Escreva("Digite o ", i, "¬∞ n√∫mero: ")
      Leia(n[i])
   FimPara

   EscrevaL()

   Para i <- 1 ate 7 faca
      Se (n[i] % 2 = 0) entao
         totPar <- totPar + 1
         EscrevaL("N√∫mero ", n[i], " na ", i, "¬∞ posicao √© PAR!")
      FimSe
   FimPara

   EscrevaL()
   EscrevaL("Total de N√∫meros Pares Digitados: ", totPar)

fimalgoritmo
```

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Litagem de Alunos Acima da M√©dia :**

<br>

- Desenvolva um algoritmo que leia o nome e duas notas de 5 alunos, calcule a m√©dia de cada um, a m√©dia da turma toda e analise se o aluno foi aprovado (nota maior ou igual a 7), reprovado (nota abaixo abaixo de 5) ou est√° em recupera√ß√£o (nota maior ou igual a 5 e menor que 7).

- Exiba na tela a m√©dia de toda a turma e uma lista com o nome dos alunos, suas respectivas m√©dias e situa√ß√£o (aprovado, reprovado ou em recupera√ß√£o)

<br>

```
algoritmo "Litagem de Alunos Acima da M√©dia"

var
  nome: vetor[1..5] de Caractere
  nota1: vetor[1..5] de Real
  nota2: vetor[1..5] de Real
  media: vetor[1..5] de Real
  status: vetor[1..5] de Caractere
  somaMedias, mediaTurma: Real
  i: Inteiro

inicio
   Para i <- 1 ate 5 faca
      EscrevaL("--------------------")
      EscrevaL("DADOS DO ALUNO ", i)
      EscrevaL("--------------------")
      Escreva("Nome: ")
      Leia(nome[i])
      Escreva("Nota 1: ")
      Leia(nota1[i])
      Escreva("Nota 2: ")
      Leia(nota2[i])

      media[i] <- (nota1[i] + nota2[i])/2

      Se (media[i] >= 7) entao
         status[i] <- "APROVADO(A)"
      senao
         Se ((media[i] >= 5) e (media[i] < 7)) entao
               status[i] <- "RECUPERA√á√ÉO"
         senao
               status[i] <- "REPROVADO(A)"
         FimSe
      FimSe

      somaMedias <- somaMedias + media[i]
   FimPara

   LimpaTela

   mediaTurma <- somaMedias / 5

   EscrevaL ("A media da turma √© ", mediaTurma:4:1)
   EscrevaL("-----------------------------------")
   EscrevaL("         Listagem de Alunos        ")
   EscrevaL("-----------------------------------")
   Para i <- 1 ate 5 faca
      EscrevaL(nome[i], "   ", media[i]:4:1, "   ", status[i])
   FimPara

fimalgoritmo
```

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - S√≥ Importa Quem Come√ßa Com C :**

<br>

- Desenvolva um Algoritmo que leia 7 nomes, coloque todos aqueles que come√ßam com "C" dentro de um vetor e depois exiba-o na tela.

<br>

```
algoritmo "S√≥ Importa Quem Come√ßa Com C"

var
  nome: Caractere
  iniciamComC: vetor[1..7] de Caractere
  c, totalComC: Inteiro

inicio
   Para c <- 1 ate 7 faca
      Escreva("Digite o ", c, "¬∞ nome: ")
      Leia(nome)

      Se (copia(maiusc(nome), 1, 1) = "C") entao
         totalComC <- totalComC + 1
         iniciamComC[totalComC] <- nome
      FimSe
   FimPara

   LimpaTela

   EscrevaL("NOMES DIGITADOS QUE COME√áAM COM C")
   EscrevaL("---------------------------------")
   Para c <- 1 ate totalComC faca
      EscrevaL(iniciamComC[c])
   FimPara

fimalgoritmo
```

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Ordena√ß√£o de Vetor :**

<br>

- Crie um algoritmo que lei 7 valores e coloque esses valores em ordem. Por√©m, deve permitir o usu√°rio escolher qual a forma de ordana√ß√£o que ser√° feita (crescente ou decrescente).

<br>

```
algoritmo "Ordena√ß√£o de Vetor"

var
   v: vetor[1..7] de Inteiro
   c, ordem: Inteiro

   Procedimento OrdemCrescente()
   var
      i, j, auxiliar: Inteiro
   inicio
      Para i <- 1 ate 6 faca
         Para j <- i+1 ate 7 faca
            Se (v[i] > v[j]) entao
               auxiliar <- v[i]
               v[i] <- v[j]
               v[j] <- auxiliar
            FimSe
         FimPara
      FimPara
   FimProcedimento

   Procedimento OrdemDecrescente()
   var
      i, j, auxiliar: Inteiro
   inicio
      Para i <- 1 ate 6 faca
         Para j <- i+1 ate 7 faca
            Se (v[i] < v[j]) entao
               auxiliar <- v[i]
               v[i] <- v[j]
               v[j] <- auxiliar
            FimSe
         FimPara
      FimPara
   FimProcedimento

inicio
    EscrevaL(" ORDENANDO UM VETOR ")
    EscrevaL("====================")

    Para c <- 1 ate 7 faca
      Escreva("Digite o ", c, "¬∞ valor: ")
      Leia(v[c])
    FimPara

    EscrevaL()
    EscrevaL("Qual forma de Ordena√ß√£o?")
    EscrevaL("[1] Ordem Crescente")
    EscrevaL("[2] Ordem Decrescente")
    Leia(ordem)
    Escolha ordem
      Caso 1
         OrdemCrescente()
      Caso 2
         OrdemDecrescente()
    FimEscolha


    EscrevaL()
    EscrevaL("-------------------------------")
    Escreva("Vetor Ordenado: ")
    Para c <- 1 ate 7 faca
      Escreva (v[c], " ")
    FimPara
    EscrevaL()
    EscrevaL("-------------------------------")

fimalgoritmo
```

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Proposto - Torneio de Futebol :**

<br>

- Desenvolva um algoritmo que leia o nome de 3 times de futebol e crie uma tabela com todas as combina√ß√µes de partidas poss√≠veis entre os tr√™s times.

<br>

```
algoritmo "Torneio de Futebol"

var
  time: vetor[1..3] de Caractere
  i, j: Inteiro

inicio
   EscrevaL("------------------")
   EscrevaL("CAMPEONATO FUTEBOL")
   EscrevaL("------------------")
   Para i <- 1 ate 3 faca
      Escreva ("Nome do ", i, "¬∞ time: ")
      Leia(time[i])
   FimPara

   LimpaTela

   EscrevaL("-------------------")
   EscrevaL(" TABELA DE PARTIDAS")
   EscrevaL("-------------------")
   Para i <- 1 ate 3 faca
      Para j <- 1 ate 3 faca
         Se (i <> j) entao
            EscrevaL(time[i]:12, " [ ] x [ ] ", time[j]:12)
         FimSe
      FimPara
   FimPara

fimalgoritmo
```

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Corrigindo Provas :**

<br>

- Crie um algoritmo que permita o usu√°rio cadastrar o gabarito de uma prova com 5 quest√µes, o nome e as respostas de 3 alunos; Calcule a nota final de cada aluno,
  a m√©dia da turma e exiba os resultados.

<br>

```
algoritmo "Corrigindo Provas"

var
   gabaritoProva: vetor[1..5] de Caractere
   gabaritoAluno: vetor[1..5] de Caractere
   nome: vetor[1..3] de Caractere
   nota: vetor[1..3] de Real
   i: Inteiro
   soma, media: Real

Procedimento CadastroGabarito()
var
   c: Inteiro
inicio
   EscrevaL("PASSO 1 - Cadastro de Gabarito ")
   EscrevaL("-------------------------------")
   Para c <- 1 ate 5 faca
      Escreva("Questao ", c, ": ")
      Leia(gabaritoProva[c])
   FimPara
FimProcedimento

Funcao CadastraProva(): Real
var
   c: Inteiro
   notaFinal: Real
inicio
   EscrevaL()
   EscrevaL("=> RESPOSTAS DADAS:")
   EscrevaL()

   Para c <- 1 ate 5 faca
      Escreva("Questao ", c, ": ")
      Leia(gabaritoAluno[c])
      Se (Maiusc(gabaritoAluno[c]) = Maiusc(gabaritoProva[c])) entao
         notaFinal <- notaFinal + 2
      FimSe
   FimPara

   Retorne notaFinal
FimFuncao

inicio
    CadastroGabarito()

    Para i <- 1 ate 3 faca
         LimpaTela
         EscrevaL("------------------------")
         EscrevaL("ALUNO ", i)
         EscrevaL("------------------------")
         Escreva("Nome: ")
         Leia(nome[i])
         nota[i] <- CadastraProva()
         soma <- soma + nota[i]
    FimPara

    LimpaTela
    EscrevaL("NOTAS FINAIS ")
    EscrevaL("-----------------------------")
    Para i <- 1 ate 3 faca
      EscrevaL(nome[i]:10, nota[i]:4:1)
    FimPara
    media <- soma / 3
    EscrevaL("-----------------------------")
    EscrevaL("Media da Turma: ", media:4:1)
fimalgoritmo
```

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Reservar Espa√ßo no Cinema :**

<br>

- Desenvolva um algoritmo que exiba uma fileira de cinema com 10 lugares e permita o usu√°rio escolher quantas cadeiras quiser. Ao usu√°rio escolher uma cadeira, 
o programa deve exbir a cadeira como ocupada e, se o usu√°rio escolher uma cadeira ocupada, deve alertar que aquele lugar j√° foi reservado.

<br>

````
algoritmo "Reservar Espa√ßo no Cinema"

var
   B: vetor[1..10] de Caractere
   numeroDaCadeira: Inteiro
   resposta: Caractere

Procedimento MostraFileira()
var i: Inteiro
inicio
   Para i <- 1 ate 10 faca
      Se (B[i] = "") entao
         Escreva ("[ B", i:2, " ]")
      senao
         Escreva ("[ --- ]")
      FimSe
   FimPara

   EscrevaL()
   EscrevaL("------------------------------------------------------------------------")
FimProcedimento

inicio
   Repita
      LimpaTela

      MostraFileira()

      Escreva("Reservar a cadeira: B")
      Leia(numeroDaCadeira)
      EscrevaL()

      Se (B[numeroDaCadeira] = "") entao
         B[numeroDaCadeira] <- "X"
         EscrevaL("Cadeira B", numeroDaCadeira, " RESERVADA!")
      Senao
         EscrevaL("ERRO: Lugar Ocupado!")
      FimSe

      EscrevaL()
      Escreva("Quer reservar outro? [S/N] ")
      Leia(resposta)
   Ate (resposta = "N")
   
fimalgoritmo
````

<br>

<a href="../README.md">Voltar</a>