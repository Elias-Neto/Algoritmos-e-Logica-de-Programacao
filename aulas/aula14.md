# üóÑ Aula 14 - Vetores (Vari√°veis Compostas - Parte 1)

<br>

> Lembrando: Durante todo este curso, ser√° utilizado o **visualg** (visualizador de algoritmos) para poder desenvolver algoritmos em **Portugol**.

<br>

> üññ Essas anota√ß√µes foram feitas a partir das aulas do professor **Gustavo Guanabara**. <br>
> üëâ Link para acessar as v√≠deo-aulas e os materiais do curso: https://www.cursoemvideo.com/curso/curso-de-algoritmo/

<br>

> At√© o momento s√≥ utilizamos vari√°veis simples. A partir de agora, iremos trabalhar com **_vari√°veis compostas_**.

<br>

### Relembrando

<br>

- ***Vari√°veis*** s√£o **espa√ßos** na mem√≥ria do computador que servem para **armazenar dados**.

- **Vari√°veis Simples** s√£o um vari√°veis que s√≥ podem armazenar um √∫nico dado.

- Vamos utilizar a analogia que utilizamos no in√≠cio do curso na qual consideramos a mem√≥ria do computador sendo um grande "arm√°rio" e os espa√ßos desse arm√°rio sendo as vari√°veis. 

- Olha s√≥ o que acontece quando declaramos e atribuimos valores em vari√°veis simples: 

<br>

<div align="center">
  <img align="center" width="500px" src="./images/vetores.gif">  
</div>

<br>

- Reparou que as vari√°veis foram **dispostas de forma BAGUN√áADA** na mem√≥ria do computador? √â justamente isso que acontece com as vari√°veis simples, elas s√£o alocadas conforme a necessidade do sistema operacional.

<br>

## **O que s√£o Vetores?**

- 

- Vetores s√£o **_Vari√°veis Compostas Homog√™neas Unidemensionais_**

- Antes de estudarmos as vetores, vamos relembrar o que s√£o as vari√°veis simples.

<br>

### **Relembrando**

<br>

- vamos pensar na seguinte analogia:a mem√≥ria do computador √© um **arm√°rio** gigante com v√°rias **gavetas**.

- essas gavetas s√£o as vari√°veis, elas tem o poder de guardar coisas (dados).

- cada gaveta dessa possui um adesivo com seu nome.

- em uma **vari√°vel simples** s√≥ √© poss√≠vel armazenar 1 valor (se quiser colocar outro valor, tem que tirar o primeiro)

```
var
    n1: inteiro
    n2, n3, n4: inteiro
inicio
    n1 <- 3
    n2 <- 5
    n3 <- 1
    n4 <- 0
```

<br>

## **Vari√°vel Simples X Vari√°vel Composta**

<br>

| Simples                            | Composta                               |
| ---------------------------------- | -------------------------------------- |
| Armazena 1 valor por vez           | Armazena valores por vez               |
| Composta por 1 espa√ßo na mem√≥ria   | Composta por v√°rios espa√ßos na mem√≥ria |
| N√£o s√£o alocadas de forma ordenada | S√£o alocadas de forma ordenada         |

<br>

### **Declarando Um Vetor**

<br>

- Exemplo: declarando um vetor n do tipo inteiro com 4 espa√ßos

```
var
    n: vetor[1..4] de inteiro
```

<br>

**Atribuindo Um Valor Num Vetor**

<br>

- Para atribuir valor em um vetor voc√™ precisa indicar o nome do vetor e a posi√ß√£o na qual voc√™ deseja armazenar o dado (chamamos de √≠ndice).

<br>

```
var
    n: vetor[1..4] de inteiro   // declarando um vetor com nome n do tipo inteiro e com 4 espa√ßos
inicio
    n[1] <- 3   // atribuindo/armazenando o valor 3, no vetor n, na posi√ß√£o 1 (1¬∞ espa√ßo do vator)
    n[2] <- 5   // atribuindo/armazenando o valor 5, no vetor n, na posi√ß√£o 2 (2¬∞ espa√ßo do vetor)
    n[3] <- 1   // atribuindo/armazenando o valor 1, no vetor n, na posi√ß√£o 3 (3¬∞ espa√ßo do vetor)
    n[4] <- 0   // atribuindo/armazenando o valor 0, no vetor n, na posi√ß√£o 4 (4¬∞ espa√ßo do vetor)
```

<br>

> Antes, utilizando vari√°veis simples, eu precisava utilizar 4 vari√°veis para armazenar 4 valores.

> Agora, utilizando vetores, eu s√≥ preciso utilizar 1 vari√°vel para armazenar 4 valores

<br>

## **Vetores - Vari√°veis Compostas Homog√™neas Unidimensionais**

- Agora que j√° sabemos um pouco mais sobre os vetores, vamos retomar √† defini√ß√£o abordada no in√≠co desse texto: "vetores s√£o vari√°veis compostas homog√™neas unidimensionais"

- Vamos por partes e destrinchar o porqu√™ dessa nome:

  - Vari√°veis: pois os vetores s√£o espa√ßos ("gavetas") na mem√≥ria do computador ("arm√°rio") que servem para armazenar dados.

  - Compostas: pois os vetores s√£o vari√°veis compostas por mais de um espa√ßo na mem√≥ria do computador.

  - Homeg√™neas: pois os espa√ßos que comp√µe um vetor s√£o do mesmo tipo.

  - Unidemensionais: pois para identificar cada um dos espa√ßos de um vetor √© necess√°rio somente um "endere√ßo" (somente um √≠ndece).

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Construindo Um Vetor :**

<br>

- Desenvolva um algoritmo que leia 5 n√∫meros, armazene-os dentro de um vetor e exiba esse vetor na tela.

<br>

```
algoritmo "Construindo Um Vetor"

var
    v: vetor[1..5] de inteiro
    i: inteiro

inicio
    Para i <- 1 ate 5 faca
        Escreva("Digete o ", i, "¬∞ n√∫mero: ")
        Leia(v[i])
    FimPara

    Para i <- 1 ate 5 faca
        Escreva("{", v[i], "}")
    FimPara

fimalgoritmo
```

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Total de Valores Par**

<br>

- Crie um algoritmo que leia sete n√∫meros, armazene-os dentro de um vetor, analise quais desses n√∫meros s√£o pares e informe qual a posi√ß√£o desses n√∫meros e o total de n√∫meros pares encontrado.

<br>

```
algoritmo "TotalPares"

var
  n: vetor[1..7] de Inteiro
  i, totPar: Inteiro

inicio
    Para i <- 1 ate 7 faca
        Escreva("Digite o ", i, "¬∞ n√∫mero: ")
        Leia(n[i])
    FimPara

    EscrevaL()

    Para i <- 1 ate 7 faca
        Se (n[i] % 2 = 0) entao
            totPar <- totPar + 1
            EscrevaL("N√∫mero ", n[i], " na ", i, "¬∞ posicao √© PAR!")
        FimSe
    FimPara

    EscrevaL()
    EscrevaL("Total de N√∫meros Pares Digitados: ", totPar)

fimalgoritmo
```

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Litagem de Alunos Acima da M√©dia :**

<br>

- Desenvolva um algoritmo que leia o nome e duas notas de 5 alunos, calcule a m√©dia de cada um, a m√©dia da turma toda e analise se o aluno foi aprovado (nota maior ou igual a 7), reprovado (nota abaixo abaixo de 5) ou est√° em recupera√ß√£o (nota maior ou igual a 5 e menor que 7).

- Exiba na tela a m√©dia de toda a turma e uma lista com o nome dos alunos, suas respectivas m√©dias, e sua situa√ß√£o (aprovado, reprovado ou em recupera√ß√£o)

<br>

```
algoritmo "ListagemAlunos"

var
  nome: vetor[1..5] de Caractere
  nota1: vetor[1..5] de Real
  nota2: vetor[1..5] de Real
  media: vetor[1..5] de Real
  status: vetor[1..5] de Caractere
  somaMedias, mediaTurma: Real
  i: Inteiro

inicio
    Para i <- 1 ate 5 faca
        EscrevaL("--------------------")
        EscrevaL("DADOS DO ALUNO ", i)
        EscrevaL("--------------------")
        Escreva("Nome: ")
        Leia(nome[i])
        Escreva("Nota 1: ")
        Leia(nota1[i])
        Escreva("Nota 2: ")
        Leia(nota2[i])
        media[i] <- (nota1[i] + nota2[i])/2
        Se (media[i] >= 7) entao
            status[i] <- "APROVADO(A)"
        senao
            Se ((media[i] >= 5) e (media[i] < 7)) entao
                status[i] <- "RECUPERA√á√ÉO"
            senao
                status[i] <- "REPROVADO(A)"
            FimSe
        FimSe
        somaMedias <- somaMedias + media[i]
    FimPara

    LimpaTela

    mediaTurma <- somaMedias / 5
    EscrevaL ("A media da turma √© ", mediaTurma:4:1)
    EscrevaL("-----------------------------------")
    EscrevaL("         Listagem de Alunos        ")
    EscrevaL("-----------------------------------")
    Para i <- 1 ate 5 faca
        EscrevaL(nome[i], "   ", media[i]:4:1, "   ", status[i])
    FimPara

fimalgoritmo
```

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - S√≥ Importa Quem Come√ßa Com C :**

<br>

- Desenvolva um Algoritmo que leia 7 nomes, coloque todos aqueles que come√ßam com "C"
  dentro de um vetor e depois exiba esses nomes na tela.

<br>

```
algoritmo "S√≥ Importa Quem Come√ßa Com C"

var
  nome: Caractere
  iniciamComC: vetor[1..7] de Caractere
  c, totalComC: Inteiro

inicio
    totalComC <- 0

    Para c <- 1 ate 7 faca
        Escreva("Digite o ", c, "¬∞ nome: ")
        Leia(nome)
        Se (copia(maiusc(nome), 1, 1) = "C") entao
            totalComC <- totalComC + 1
            iniciamComC[totalComC] <- nome
        FimSe
    FimPara

    LimpaTela
    EscrevaL("NOMES DIGITADOS QUE COME√áAM COM C")
    EscrevaL("---------------------------------")
    Para c <- 1 ate totalComC faca
        EscrevaL(iniciamComC[c])
    FimPara

fimalgoritmo
```

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Ordena√ß√£o de Vetor :**

<br>

- Crie um algoritmo que lei 7 valores e coloque esses valores em ordem. Deve permitir o
  usu√°rio escolher qual forma de ordana√ß√£o ser√° feita (crescente ou decrescente).

<br>

```
algoritmo "Ordena√ß√£o de Vetor"

var
  v: vetor[1..7] de Inteiro
  c, ordem: Inteiro

Procedimento OrdemCrescente()
var
   i, j, auxiliar: Inteiro
inicio
   Para i <- 1 ate 6 faca
      Para j <- i+1 ate 7 faca
         Se (v[i] > v[j]) entao
            auxiliar <- v[i]
            v[i] <- v[j]
            v[j] <- auxiliar
         FimSe
      FimPara
   FimPara
FimProcedimento

Procedimento OrdemDecrescente()
var
   i, j, auxiliar: Inteiro
inicio
   Para i <- 1 ate 6 faca
      Para j <- i+1 ate 7 faca
         Se (v[i] < v[j]) entao
            auxiliar <- v[i]
            v[i] <- v[j]
            v[j] <- auxiliar
         FimSe
      FimPara
   FimPara
FimProcedimento

inicio
    EscrevaL(" ORDENANDO UM VETOR ")
    EscrevaL("====================")


    Para c <- 1 ate 7 faca
        Escreva("Digite o ", c, "¬∞ valor: ")
        Leia(v[c])
    FimPara

    EscrevaL()
    EscrevaL("Qual forma de Ordena√ß√£o?")
    EscrevaL("[1] Ordem Crescente")
    EscrevaL("[2] Ordem Decrescente")
    Leia(ordem)
    Escolha ordem
        Caso 1
            OrdemCrescente()
        Caso 2
            OrdemDecrescente()
    FimEscolha


    EscrevaL()
    EscrevaL("-------------------------------")
    Escreva("Vetor Ordenado: ")
    Para c <- 1 ate 7 faca
        Escreva (v[c], " ")
    FimPara
    EscrevaL()
    EscrevaL("-------------------------------")

fimalgoritmo
```

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Proposto - Torneio de Futebol :**

<br>

- Desenvolva um algoritmo que leia o nome de 3 times de futebol e crie uma tabela
  com todas as combina√ß√µes de partidas poss√≠veis entre os tr√™s times.

<br>

```
algoritmo "Torneio de Futebol"

var
  time: vetor[1..3] de Caractere
  i, j: Inteiro

inicio
   EscrevaL("------------------")
   EscrevaL("CAMPEONATO FUTEBOL")
   EscrevaL("------------------")
   Para i <- 1 ate 3 faca
      Escreva ("Nome do ", i, "¬∞ time: ")
      Leia(time[i])
   FimPara

   LimpaTela
   EscrevaL("-------------------")
   EscrevaL(" TABELA DE PARTIDAS")
   EscrevaL("-------------------")
   Para i <- 1 ate 3 faca
      Para j <- 1 ate 3 faca
         Se (i <> j) entao
            EscrevaL(time[i]:12, " [ ] x [ ] ", time[j]:12)
         FimSe
      FimPara
   FimPara

fimalgoritmo
```

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Corrigindo Provas :**

<br>

- Crie um algoritmo que permita o usu√°rio cadastrar o gabarito de uma prova com
  5 quest√µes, o nome e as respostas de 3 alunos; Calcule a nota final de cada aluno,
  a m√©dia da turma e exiba os resultados.

<br>

```
algoritmo "Corrigindo Provas"

var
   gabarito: vetor[1..5] de Caractere
   prova: vetor[1..5] de Caractere
   nome: vetor[1..3] de Caractere
   nota: vetor[1..3] de Real
   i: Inteiro
   soma, media: Real

Procedimento CadastroGabarito()
var
   c: Inteiro
inicio
   EscrevaL("PASSO 1 - Cadastro de Gabarito ")
   EscrevaL("-------------------------------")
   Para c <- 1 ate 5 faca
      Escreva("Questao ", c, ": ")
      Leia(gabarito[c])
   FimPara
FimProcedimento

Funcao CadastraProva(): Real
var
   c: Inteiro
   notaFinal: Real
inicio
    notaFinal <- 0
    EscrevaL("RESPOSTAS DADAS")
    Para c <- 1 ate 5 faca
      Escreva("Questao ", c, ": ")
      Leia(prova[c])
      Se (Maiusc(prova[c]) = Maiusc(gabarito[c])) entao
         notaFinal <- notaFinal + 2
      FimSe
   FimPara
   Retorne notaFinal
FimFuncao

inicio
    CadastroGabarito()

    Para i <- 1 ate 3 faca
         LimpaTela
         EscrevaL("------------------------")
         EscrevaL("ALUNO ", i)
         EscrevaL("------------------------")
         Escreva("Nome: ")
         Leia(nome[i])
         nota[a] <- CadastraProva()
         soma <- soma + nota[i]
    FimPara

    LimpaTela
    EscrevaL("NOTAS FINAIS ")
    EscrevaL("-----------------------------")
    Para i <- 1 ate 3 faca
       EscrevaL(nome[i]:10, nota[i]:4:1)
    FimPara
    media <- soma / 3
    EscrevaL("-----------------------------")
    EscrevaL("Media da Turma: ", media:4:1)
fimalgoritmo
```

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Reservar Espa√ßo no Cinema :**

<br>

- Desenvolva um algoritmo que exiba uma fileira de cinema com 10 lugares, permita
o usu√°rio escolher quantas cadeiras ele quiser. Ao usu√°rio escolher uma cadeira, 
o programa deve exbir a cadeira como ocupada e, se o usu√°rio escolher uma cadeira ocupada,
deve alertar que aquele lugar j√° foi reservado.

<br>

````
algoritmo "Reservar Espa√ßo no Cinema"

var
   B: vetor[1..10] de Caractere
   numeroDaCadeira: Inteiro
   resposta: Caractere
   
Procedimento MostraFileira()
var i: Inteiro
inicio
   Para i <- 1 ate 10 faca
      Se (B[i] = "") entao
         Escreva ("[ B", i:2, " ]")
      senao
         Escreva ("[ --- ]")
      FimSe
   FimPara
   EscrevaL()
   EscrevaL("------------------------------------------------------------------------")
FimProcedimento

inicio
   Repita
      LimpaTela
      MostraFileira()
      Escreva("Reservar a cadeira: B")
      Leia(numeroDaCadeira)
      Se (B[numeroDaCadeira]="") entao
         B[numeroDaCadeira] <- "X"
         EscrevaL("Cadeira B", numeroDaCadeira, " RESERVADA!")
      Senao
         EscrevaL("ERRO: Lugar Ocupado!")
      FimSe
      Escreva("Quer reservar outro? [S/N] ")
      Leia(resposta)
   Ate (resposta = "N")
fimalgoritmo
````
