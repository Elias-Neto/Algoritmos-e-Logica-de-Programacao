# ğŸ—„ğŸ—„ Aula 15 - Matrizes (VariÃ¡veis Compostas - Parte 2)

<br>

> Lembrando: Durante todo este curso, serÃ¡ utilizado o **visualg** (visualizador de algoritmos) para poder desenvolver algoritmos em **Portugol**.

<br>

> ğŸ–– Essas anotaÃ§Ãµes foram feitas a partir das aulas do professor **Gustavo Guanabara**. <br>
> ğŸ‘‰ Link para acessar as vÃ­deo-aulas e os materiais do curso: https://www.cursoemvideo.com/curso/curso-de-algoritmo/

<br>

## **O que sÃ£o Matrizes?**

- Matrizes, antes de qualquer coisa, sÃ£o **variÃ¡veis** (ou seja, servem para armazenar dados na memÃ³ria do computador). PorÃ©m,  assim como os vetores, nÃ£o sÃ£o variÃ¡veis simples mas sim **variÃ¡veis compostas** (ou seja, sÃ£o compostas por mais de um espaÃ§o na memÃ³ria do computador).

- O funcionamento das matrizes possui pontos semelhantes ao dos vetores, contudo, o diferencial das matrizes Ã© a capacidade delas de possuirem **mais de uma dimensÃ£o**. 

- Vamos com calma... Para entender esse conceito de mais de uma dimensÃ£o das matrizes, vamos reelembrar alguns pontos vistos sobre os vetores:

<br>

<div align="center">
  <img align="center" width="500px" src="./images/matrizes.gif">  
</div>

<br>

- Ã‰ importante prestarmos atenÃ§Ã£o nos seguintes pontos:

   1 - A forma de **declaraÃ§Ã£o** do vetor. {nomeDaVariavel + vetor[quantidadeDeEspaÃ§osDisponÃ­veis] + tipoDaVariavel}

   2 - A forma na qual o vetor Ã© **disposto** na memÃ³ria do computador. {forma ordenada e sequencial em linha}

   3 - A forma de **atribuir** valores no vetor. {nomeDaVariavel[inidice]}


- Show! Tendo isso em mente, vamos observar como esses e outros fatoes funcionam nas MATRIZES.

<div align="center">
  <img align="center" width="500px" src="./images/matrizes(1).gif">  
</div>

- Olha que interessante:

  - Na **declaraÃ§Ã£o**, apesar de ser quase 100% igual a de um vetor, numa matriz existem **dois** elementos para indicar a **quantidade de espaÃ§os disponÃ­veis**.

    - Esse Ã© justamente o diferencial das matrizes citado anteriormente...
    
    - O primeiro elemento indica a quantidade de espaÃ§os disponÃ­veis em uma das dimensÃ£o da matriz (as **LINHAS**).
    
    - E o segundo elemento indica a quantidade de espaÃ§os disponÃ­veis na outra dimensÃ£o da matriz (as **COLUNAS**).

  - A **disposiÃ§Ã£o** de uma matriz na memÃ³ria do computador, consequentemente, ocuparÃ¡ **mais espaÃ§os**, porÃ©m, continua sendo **organizada** e **sequencial** (em LINHAS e COLUNAS).

  - Outra consequÃªncia da multidimensionalidade de uma matriz ocorre na hora da **atribuiÃ§Ã£o** de valores dentro dela. 

    - Ã‰ necessÃ¡rio informar nÃ£o 1 Ãºnico Ã­ndece (como acontece com os vetores) mas sim **2 ÃNDECES**,

      - O primeiro Ã­ndice indica a posiÃ§Ã£o na qual o valor irÃ¡ ser atribuÃ­do em relaÃ§Ã£o as linhas (1Â° dimensÃ£o).

      - O segundo Ã­ndice indica a posiÃ§Ã£o na qual o valor irÃ¡ ser atribuÃ­do em relaÃ§Ã£o as colunas (2Â° dimensÃ£o).

<br>

## Matrizes - VariÃ¡veis Compostas HomogÃªneas Multimensionais

- As matrizes tambÃ©m possuem um nome tÃ©cnico (bem semelhante ao dos vetores). Elas sÃ£o chamadas de: VariÃ¡veis Compostas HomogÃªneas Multidimensionais

- Os conceitos sÃ£o os mesmos, ambas (os vetores e as matrizes) sÃ£o variÃ¡veis compostas homogÃªneas. A Ãºnica diferenÃ§a sÃ£o quatro letrinhas: os vetores se caracterizam por serem **UNI**dimensionais (possuem apenas **uma dimensÃ£o**, logo, sua atribuiÃ§Ã£o de valor funciona com somente **um Ã­ndice**), jÃ¡ as matrizes por serem **MULTI**dimensionais (possuem **mais de uma dimensÃ£o**, logo, sua atribuiÃ§Ã£o de valor funciona com **mais de um Ã­ndice**).

<br>

> OBSâ— Os conceitos matemÃ¡ticos de matrizes, tambÃ©m valem para as matrizes as quais vamos trabalhar.

<br>

### ğŸ‹ï¸â€â™‚ï¸ **ExercÃ­cio PrÃ¡tico - Construindo Uma Matriz :**

<br>

- Desenvolva um algoritmo que leia seis nÃºmeros, coloque-os dentro de uma matriz 3x2 (3 linhas e 2 colunas) e exiba essa matriz.

<br>

```
algoritmo "Construindo Uma Matriz"

var
  m: vetor[1..3, 1..2] de inteiro
  linha, coluna: inteiro

inicio
  EscrevaL("Construindo Uma Matriz")
  EscrevaL("======================")
  
  Para linha <- 1 ate 3 faca
    Para coluna <- 1 ate 2 faca
      Escreva("Digite o valor da posiÃ§Ã£o [", linha, ",", coluna, "] : ")
      Leia(m[linha, coluna])
    FimPara
  FimPara

  EscrevaL()
  Para linha <- 1 ate 3 faca
    Para coluna <- 1 ate 2 faca
      Escreva(m[linha, coluna]:5)
    FimPara
    EscrevaL()
  FimPara

fimalgoritmo
```

<br>

### ğŸ‹ï¸â€â™‚ï¸ **ExercÃ­cio PrÃ¡tico - Construir uma Matriz e Mostrar os NÃºmeros Pares :**

<br>

- Desenvolva um algoritmo que leia nove nÃºmeros, coloque-os dentro de uma matriz 3x3 (3 linhas e 3 colunas), exiba essa matriz e destaque os nÃºmeros que forem pares.

<br>

```
algoritmo "Construir uma Matriz e Mostrar os NÃºmeros Pares"

var
  m: vetor[1..3, 1..3] de Inteiro
  linha, coluna, totalPares: Inteiro
  
inicio
  Para linha <- 1 ate 3 faca
    Para coluna <- 1 ate 3 faca
      Escreva ("Digite o valor da posicao [", linha, ",", coluna, "]: ")
      Leia(m[linha, coluna])
    FimPara
  FimPara

  EscrevaL()
  EscrevaL("MATRIZ")
  EscrevaL("------")
  
  Para linha <- 1 ate 3 faca
    Para coluna <- 1 ate 3 faca
       Se(m[linha, coluna] % 2 = 0) entao
         Escreva("{", m[linha, coluna]:2, "}")
         totalPares <- totalPares + 1
       senao
         Escreva(m[linha, coluna]:4)
       fimSe
    FimPara
    Escreval()
  FimPara

  EscrevaL()
  EscrevaL("Ao todo foram digitados ", totalPares, " nÃºmeros PARES")
  
fimalgoritmo
```

<br>

### ğŸ‹ï¸â€â™‚ï¸ **ExercÃ­cio PrÃ¡tico - Criar uma Matriz Identidade de 3Â° Ordem :**

<br>

- Desenvolva um algoritmo que crie uma matriz identidade de 3Â° ordem (3x3).

- Matriz identidade Ã© uma matriz cuja diagonal principal (formada por posiÃ§Ãµes nas quais o nÃºmero da linha Ã© igual ao da coluna) sÃ³ tem valor 1 e as demais posiÃ§Ãµes tÃªm valor 0.

````
algoritmo "Criar uma Matriz Identidade de 3Â° Ordem"

var
  m: vetor[1..3, 1..3] de Inteiro
  i, j: Inteiro

inicio
  Para i <- 1 ate 3 faca
    Para j <- 1 ate 3 faca
      Se (i = j) entao
        m[i,j] <- 1
      senao
        m[i,j] <- 0
      FimSe
    FimPara
  FimPara
  
  Para i <- 1 ate 3 faca
    Para j <- 1 ate 3 faca
      Escreva (m[i,j]:3)
    FimPara
    EscrevaL()
  FimPara
    
fimalgoritmo
````

<br>

### ğŸ‹ï¸â€â™‚ï¸ **ExercÃ­cio PrÃ¡tico - Brincando Com Uma Matriz de 4Â° ordem :**

<br>

- Crie um algoritmo que leia dezesseis nÃºmeros, coloque-os dentro de uma matriz de 4Â° ordem (4x4) e,

  - Exiba essa matriz;

  - Calcule e mostre a soma dos valores da diagonal principal;

  - Calcule e mostre o produto entre os valores da segunda linha;

  - Identifique e mostre o maior valor da terceira coluna.

````
algoritmo "Brincando Com Uma Matriz de 4Â° ordem"

var
  m: vetor[1..4, 1..4] de Inteiro
  i, j, somaDiagonalPrincipal, produtoSegundaLinha, maiorTerceiraColuna: Inteiro

inicio
  produtoSegundaLinha <- 1
  Para i <- 1 ate 4 faca
    Para j <- 1 ate 4 faca
      Escreva("Digite o valor da Posicao [", i, ",", j, "]: ")
      Leia(m[i, j])
      Se (i = j) entao
        somaDiagonalPrincipal <- somaDiagonalPrincipal + m[i ,j]
      FimSe
    FimPara
  FimPara

  EscrevaL()
  Para i <- 1 ate 4 faca
    Para j <- 1 ate 4 faca
      Escreva(m[i, j]:4)
    FimPara
    EscrevaL()
  FimPara
  EscrevaL()

  Para i <- 1 ate 4 faca
    produtoSegundaLinha <- produtoSegundaLinha * m[2, i]
  FimPara

  Para i <- 1 ate 4 faca
    Se (m[i, 3] > maiorTerceiraColuna) entao
      maiorTerceiraColuna <- m[i, 3]
    FimSe
  FimPara

  EscrevaL("A soma dos valores da Diagonal Principal e ", somaDiagonalPrincipal)
  EscrevaL("O produto dos valores da Segunda Linha e ", produtoSegundaLinha)
  EscrevaL("O maior valor da Terceira Coluna e ", maiorTerceiraColuna)
  
fimalgoritmo
````

<br>

### ğŸ‹ï¸â€â™‚ï¸ **ExercÃ­cio PrÃ¡tico - Dissecando Matrizes :**

<br>

- Desenvolva um algoritmo para preencher uma matriz de quarta ordem e mostrar:

  - A matriz completa;

  - Os elementos da diagonal principal;

  - Os elementos do triangulo superior (o que sobra da diagonal principal na parte de cima);

  - Os elementos do triangulo inferior (o que sobra da diagonal principal na parte de baixo).

````
algoritmo "Dissecando Matrizes"

var
  m: vetor[1..4, 1..4] de Inteiro
  linha, coluna, opcao: Inteiro

Procedimento MostraMatriz()
inicio
  Para linha <- 1 ate 4 faca
    Para coluna <- 1 ate 4 faca
      Escreva(m[linha, coluna]:4)
    FimPara
    EscrevaL()
  FimPara
FimProcedimento

Procedimento DiagonalPrincipal()
var espaco: inteiro
inicio
  Para linha <- 1 ate 4 faca
    EscrevaL(m[linha , linha]:4)
    Para espaco <- 1 ate linha faca
      Escreva("    ")
    FimPara
  FimPara
FimProcedimento

Procedimento TrianguloSuperior()
var espaco: Inteiro
inicio
  Para linha <- 1 ate 3 faca
    Escreva("    ")
    Para coluna <- linha+1 ate 4 faca
      Escreva(m[linha, coluna]:4)
    FimPara
    EscrevaL()
    Para espaco <- 1 ate linha faca
      Escreva("    ")
    FimPara
  FimPara
  EscrevaL()
FimProcedimento

Procedimento TrianguloInferior()
inicio
  Para linha <- 2 ate 4 faca
    EscrevaL()
    Para coluna <- 1 ate linha-1 faca
      Escreva(m[linha, coluna]:4)
    FimPara
  FimPara
  EscrevaL()
FimProcedimento

inicio
  Para linha <- 1 ate 4 faca
    Para coluna <- 1 ate 4 faca
      Escreva("Digite valor para a posicao [", linha, ",", coluna, "]: ")
      Leia(m[linha, coluna])
    FimPara
  FimPara

  LimpaTela

  Repita
    EscrevaL()
    EscrevaL("MENU DE OPCOES")
    EscrevaL("====================")
    EscrevaL("[1] Mostrar a Matriz")
    EscrevaL("[2] Diagonal Principal")
    EscrevaL("[3] Triangulo Superior")
    EscrevaL("[4] Triangulo Inferior")
    EscrevaL("[5] Sair")
    Escreva("===== OPCAO: ")
    Leia(opcao)
    LimpaTela
    Escolha opcao
        Caso 1
          MostraMatriz()
        Caso 2
          DiagonalPrincipal()
        Caso 3
          TrianguloSuperior()
        Caso 4
          TrianguloInferior()
        Caso 5
          EscrevaL("ENCERRANDO...")
    FimEscolha
  Ate (opcao = 5)

fimalgoritmo
````

<br>

### ğŸ‹ï¸â€â™‚ï¸ **ExercÃ­cio PrÃ¡tico - Jogo da Velha :**

<br>

- Programe um algoritmo que crie um jogo da velha funcional.

````
algoritmo "Jogo da Velha"

var
  m: vetor[1..3, 1..3] de Caractere
  linha, coluna, i: Inteiro
  simbolo, posicao: Caractere
  jogada: Logico

Procedimento mostraVelha()
Inicio
  EscrevaL("+---+---+---+")
  Para linha <- 1 ate 3 faca
    Para coluna <- 1 ate 3 faca
      Escreva("|  ", m[linha, coluna]:1)
    FimPara
    Escreva("|")
    EscrevaL()
    EscrevaL("+---+---+---+")
  FimPara
FimProcedimento

Procedimento mudaJogador()
inicio
   Se (simbolo = "X") entao
      simbolo <- "O"
   senao
      simbolo <- "X"
   FimSe
FimProcedimento

Funcao Jogar(S: Caractere; P: Caractere): Logico
Var
  mudanca: logico
inicio
   Para linha <- 1 ate 3 faca
      Para coluna <- 1 ate 3 faca
        Se(m[linha, coluna] = P) entao
          m[linha, coluna] <- S
          mudanca <- verdadeiro
        FimSe
      FimPara
   FimPara

   Retorne mudanca
FimFuncao

Funcao terminouVelha(): Logico
Var
  terminou: Logico
  espacoNaoPreenchido: Inteiro
inicio
   // Jogos em linha
   Para linha <- 1 ate 3 faca
      Se (m[linha, 1] = m[linha, 2]) e (m[linha, 2] = m[linha, 3]) entao
         terminou <- verdadeiro
      FimSe
   FimPara

   // Jogos em coluna
   Para coluna <- 1 ate 3 faca
      Se (m[1, coluna] = m[2,coluna]) e (m[2, coluna] = m[3, coluna]) entao
         terminou <- verdadeiro
      FimSe
   FimPara

   // Jogos em diagonal
   Se (m[1,1] = m[2,2]) e (m[2,2] = m[3,3]) entao
      terminou <- verdadeiro
   FimSe
   Se (m[1,3] = m[2,2]) e (m[2,2] = m[3,1]) entao
      terminou <- verdadeiro
   FimSe

   // Jogos em VELHA
   Para linha <- 1 ate 3 faca
      Para coluna <- 1 ate 3 faca
         Se (m[linha, coluna] <> "X") e (m[linha, coluna] <> "O") entao
            espacoNaoPreenchido <- espacoNaoPreenchido + 1
         FimSe
      FimPara
   FimPara
   Se (espacoNaoPreenchido = 0) entao
      terminou <- verdadeiro
   FimSe

   // Retorna se o jogo acabou ou nao
   Retorne terminou
FimFuncao

inicio
  i <- 1
  simbolo <- "X"

  Para linha <- 1 ate 3 faca
    Para coluna <- 1 ate 3 faca
      m[linha, coluna] <- NumpCarac(i)
      i <- i + 1
    FimPara
  FimPara

  MostraVelha()

  Repita
    Repita
      Escreva("Vai jogar [", simbolo:1, "] em qual posicao? ")
      Leia(posicao)

      jogada <- Jogar(simbolo, posicao)

      Se (jogada = falso) entao
        EscrevaL("JOGADA INVALIDA!")
      FimSe
    Ate (jogada = verdadeiro)

    MudaJogador()
    LimpaTela
    MostraVelha()
  Ate (terminouVelha() = verdadeiro)

  EscrevaL("JOGO FINALIZADO!!!")
  
fimalgoritmo
````
