# üõ† Aula 13 - Fun√ß√µes

> Lembrando: Durante todo este curso, ser√° utilizado o **visualg** (visualizador de algoritmos) para poder desenvolver e executar os algoritmos em **Portugol**.

<br>

## **Rotina (Parte 02) - Fun√ß√µes**

<br>

**Relembrando**

- rotina √© um procedimento repetitivo

- rotinas s√£o ut√©is para evitarmos repeti√ß√£o de c√≥digo

<br>

**Procedimento x Fun√ß√£o**

- Nos ***Procedimentos*** n√£o se retorna um valor, neles ocorre simplesmente a execu√ß√£o de
um bloco de c√≥digo

- Nas ***Fun√ß√µes*** se retorna um valor, nelas um bloco de c√≥digo √© executado mas o objetivo
√© retornar um valor ao final da fun√ß√£o.

<br>

**Sintaxe da Estrutura**

````
Funcao nomeDaFuncao(): tipoDaFuncao
inicio
  Bloco
  Retorne()
FimFuncao
````

<br>

**Exemplo - Somar Dois N√∫meros (Procedimento x Fun√ß√£o) :**

````
algoritmo "somar dois n√∫meros com PROCEDIMENTO"

var
  numero1, numero2: Inteiro
  
  Procedimento Soma(A, B: Inteiro)
  Inicio
     Escreval("A soma entre ", A, " e ", B, " √© igual a ", A+B)
  FimProcedimento

inicio
  Escreva("Digite o primeiro valor: ")
  Leia(numero1)
  Escreva("Digite o segundo valor: ")
  Leia(numero2)
  Soma(numero1, numero2)

fimalgoritmo
````

````
algoritmo "somar dois n√∫meros com FUN√á√ÉO"

var
  numero1, numero2, soma: Inteiro
  
  Funcao Soma(A, B: Inteiro): Inteiro
  Inicio
    Retorne(A+B)
  FimFuncao

inicio
  Escreva("Digite o primeiro valor: ")
  Leia(numero1)
  Escreva("Digite o segundo valor: ")
  Leia(numero2)
  soma <- Soma(numero1, numero2)
  Escreval("A soma entre ", numero1, " e ", numero2, " √© igual a ", soma)

fimalgoritmo
````

## **Passagem de Par√¢metro**

- Assim como nos procedimentos, em fun√ß√µes voc√™ tamn√©m pode utilizar par√¢metros.

- O funcionamento e o tipo de passagens de par√¢metros dos procedimentos √© o mesmo para as fun√ß√µes.

<br>

**Relembrando**

- Par√¢metros s√£o vari√°veis que est√£o recebem alguma coisa como refer√™ncia.

<br>

## **Passagem de Par√¢metro Por Valor**

<br>

**Relembrando**

- √â um tipo de passagem na qual os par√¢metros do procedimento recebem valores como refer√™ncia

- OBS‚ùï N√£o use o mesmo nome da vari√°vel refer√™ncia no par√¢metro.

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Verificador Par/√çmpar :**

- Desenvolva um algoritmo que leia um n√∫mero, verifique e informe se ele √© par ou √≠mpar.

- Utilize uma fun√ß√£o para realizar essa verifica√ß√£o.

````
algoritmo "VERIFICADOR PAR/√çMPAR"
var
  numero: Inteiro
  retorno: Caractere
   
Funcao ParOuImpar(N: Inteiro): Caractere
Inicio
  Se (N%2 = 0) entao
    Retorne("PAR")
  senao
    Retorne("√çMPAR")
  FimSe
FimFuncao

inicio
  Escreva("Digite um n√∫mero: ")
  Leia(numero)
  retorno <- ParOuImpar(numero)
  EscrevaL("O n√∫mero ", numero, " √© um valor ", retorno)
fimalgoritmo
````

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Fatorial de Um N√∫mero :**

- Crie um algoritmo que leia um n√∫mero, calcule e informe o fatorial desse n√∫mero.

- Utilize uma fun√ß√£o para realizar essa c√°lculo.

````
algoritmo "FATORIAL DE UM N√öMERO"
var
  numero, F: Inteiro

Funcao Fatorial(N:Inteiro): Inteiro
var
  contador, resultado: Inteiro
Inicio
  Resultado <- 1
  Para Contador <- 1 ate N faca
    resultado <- resultado * Contador
  FimPara
  Retorne resultado
FimFuncao

inicio
  Escreva("Digite um n√∫mero: ")
  Leia(numero)
  F <- Fatorial(numero)
  Escreval("O valor de ", numero, "! √® igual a ", F)
fimalgoritmo
````

<br>

## **Passagem de Par√¢metro Por Refer√™ncia**

<br>

**Relembrando**

- Nesse tipo de passagem, os par√¢metros receber√£o a pr√≥pria vari√°vel como refer√™ncia.

- Podemos fazer uma analogia e dizer que os par√¢metros receber√£o o 'endere√ßo' da vari√°vel 
como refer√™ncia.

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Sequ√™ncia de Fibonacci :**

- Desenvolva um algoritmo que calcule e exiba na tela os 15 primeiros n√∫meros da sequ√™ncia de fibonacci.

- Utilize uma fun√ß√£o para realizar esse c√°lculo.

````
algoritmo "SEQU√äNCIA DE FIBONACCI"
var
  contador, valor1, valor2, valor3: Inteiro

Funcao ProximoFibonacci(var V1, V2: Inteiro):Inteiro
var
  V3: Inteiro
Inicio
  V3 <- V1 + V2
  V1 <- V2
  V2 <- V3
  Retorne(V3)
FimFuncao

inicio
  Escreva(valor1)
  valor2 <- 1
  Escreva(valor2)
  Para contador <- 3 ate 15 faca
    valor3 <- ProximoFibonacci(valor1, valor2)
    Escreva(valor3)
  FimPara
fimalgoritmo
````

<br>

## **Fun√ß√µes do Visualg - valor carctere**

<br>

 **site <- "CursoEmVideo"**

 <br>

Fun√ß√£o | Retorno | O que faz?
:--------- | :------: | -------:
Compr(site) | 12 | Retorna o comprimento de uma string
Copia(site, 6, 2) | Em | Retorna a copia de uma string contando a partir de um intervalo (string, inicioDoIntervalo, quantidadeDeLetras)
Maiusc(site) | CURSOEMVIDEO | Coloca todas as letras de uma string em mai√∫sculas
Minusc(site) | cursoemvideo | Coloca todas as letras de uma string em min√∫sculas
Pos("Video", site) | 8 | Retorna a posi√ß√£o de uma determinada string ou letra dentro de uma string maior
Asc("C") | 67 | Retorna o c√≥digo espec√≠fico de uma letra
Carac("67") | C | Retorna a letra espec√≠fica de um c√≥digo

<br>

### üèãÔ∏è‚Äç‚ôÇÔ∏è **Exerc√≠cio Pr√°tico - Analisador de Nomes :**

- Desenvolva um algoritmo, utilizando as fun√ß√µes do visualg, que leia o nome do usu√°rio e:
  
  - Informe o total de letras do nome
  
  - Exiba o nome em letras mai√∫sculas
  
  - Exiba o nome em letras min√∫sculas
  
  - Informe a primeira letra do nome
  
  - Informe a √∫ltima letra do nome
  
  - Informe a posi√ß√£o da letra "A" no nome
  
  - Informe a c√≥digo correspondente a letra "A"
  
  - Informe a a letra correspondnete ao c√≥digo 65

  - Monte e exiba na tela o nome do usu√°rio ao contr√°rio

````
algoritmo "ANALISADOR DE NOMES"
var
  nome: Caractere
  Contador: Inteiro
inicio
  Escreva("Digite seu nome: ")
  Leia(nome)
  Escreval("Total de letras do seu nome √© ", Compr(nome))
  Escreval("Seu nome em mai√∫sculas ", Maiusc(nome))
  Escreval("Seu nome em min√∫sculas √© ", Minusc(nome))
  Escreval("A primeira letra do seu nome √© ", Copia(Maiusc(nome), 1, 1))
  Escreval("A √∫ltima letra do seu nome √© ", Copia(Maiusc(nome), Compr(nome), 1))
  Escreval("Seu nome tem a letra A na posi√ß√£o ", Pos("A", Maiusc(nome)))
  Escreval("O c√≥digo da letra A √© ", Asc("A"))
  Escreval("A letra de c√≥digo 65 √© ", Carac(65))
  Escreva("Seu nome ao contr√°rio √© ")
  Para Contador <- Compr(nome) ate 1 passo -1 faca
    Escreva(Copia(Maiusc(nome), Contador, 1))
  FimPara
fimalgoritmo
````

<br>

<p align="center"> Desenvolvido com üíô por Elias de Ara√∫jo Ferreira Neto üëã <p>